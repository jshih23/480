#!/usr/bin/perl -w

use strict;
my $debug = 0;

my $CLASS  = "480";
my $HWK    = "1";
my $NAME   = "nfind";           # Name of program
my $TEST   = "nIJ";             # Name of test file (IJ is replaced by number)
my $ANSWER = "nIJ.t";           # Name of answer file (IJ is replaced by number)
my $DATE   = "09/16/17";        # Date script written
my $LANG   = "C";               # Language (C, Perl, ...)

# Blank-separated list of illegal files; wildcards permitted
my $hwkFiles = "";

my $PROGRAM = "./$NAME";        # Name of executable

my %WHICH;                      # Allow individual tests on command line
@WHICH{@ARGV}++
   if (@ARGV);

$SIG{HUP} = $SIG{INT} = $SIG{QUIT}
   = $SIG{TERM} = \&cleanup;
my @UNLINK;                                     # Files to delete on signal
my $TMPDIR = "/tmp/TEST.$NAME.$$";              # Name of temporary directory
sub cleanup {
   unlink (@UNLINK);                            # Delete files
   if (-e $TMPDIR) {                            # Delete temporary directory
      system ("/bin/chmod -R +w $TMPDIR");
      system ("/bin/rm -rf $TMPDIR");
   }
   exit;
}

my $WallClock = 0;

$0 =~ s{^.*/}{};                                # Extract test type
my $TYPE = ($0 =~ m{^test\.}) ? "Public" : "Final";
print "\n$TYPE test script for $NAME ($DATE)\n\n";

&makeProgram
   unless ($LANG eq "Perl");

&limitCpuTime (2, 4);                           # Limit CPU-time per process
&limitWallClock (10);                           # Limit wall-clock per process
&limitFileSize (100000);                        # Limit size of files created
&limitHeapSize (1000000);                       # Limit size of heap
&limitProcesses (1000);                         # Limit #processes

$|++;
print "\nEach test is either passed or failed; there is no partial credit.\n\n"
    . "To execute the test labelled IJ, type the command:\n"
    . "     /c/cs$CLASS/Hwk${HWK}/Tests/$TEST\n"
    . "The answer expected is in /c/cs$CLASS/Hwk${HWK}/Tests/$ANSWER.\n\n";

my $total = 0;

&header ("Basic Functionality");
$total += &runTest ("01", "Many files/directories");

&header ("-P & -L");
$total += &runTest ("02", "Symbolic links; last -P / -L prevails");

&header ("-depth");
$total += &runTest ("03", "Post-order traversal?");

&header ("-maxdepth");
$total += &runTest ("04", "maxdepth = 2");

&header ("-exec");
$total += &runTest ("05", "Command with no {}");
$total += &runTest ("06", "Command with one argument = {}");

&header ("-name");
$total += &runTest ("07", "Regular expression matches");

&header ("-newer");
$total += &runTest ("08", "Local file newer or older");

&header ("-a / -o");
$total += &runTest ("09", "Simple -a");
$total += &runTest ("10", "Simple -o");

&header ("Detect Loops");
$total += &runTest ("11", "Simple [two-hop: a -> b -> a]");

&header ("Errors");
$total += &runTest ("12", "Invalid command-line options");
$total += &runTest ("13", "Invalid maxdepth: not integer (abc)");
$total += &runTest ("14", "Nonexistent or unreadable file on command line");

&header ("Torture");
$total += &runTest ("15", "No actions (default is -print)");
$total += &runTest ("16", ".files");

&header ("Free All Storage");
$total += &runTest ("17", "Basic Functionality: Many files/directories");
$total += &runTest ("18", "Nonexistent or unreadable file on command line");

# &header ('Deductions for Violating Specification (0 => no violation)');
# $total += &deduction (localCopies($hwkFiles), "Local copy of $hwkFiles");

print "\nEnd of Public Script\n";

&header ("-maxdepth");
$total += &runTest ("19", "maxdepth = 0; maxdepth = 1; last maxdepth prevails");
$total += &runTest ("20", "VERY large value for MAXDEPTH");

&header ("-exec");
$total += &runTest ("21", "{} in middle of argument or in multiple arguments");
$total += &runTest ("22", "Multiple {}'s in argument, including side by side");
$total += &runTest ("23", "Status can be nonzero");
$total += &runTest ("24", "-print is not default; command with 'keyword' argument");
$total += &runTest ("25", "Multiple -exec's");
$total += &runTest ("26", "Commmand with embedded/isolated {} meets file named {}");

&header ("-name");
$total += &runTest ("27", "Test is on local name, not full name; multiple -name's");

&header ("-newer");
$total += &runTest ("28", "Equal times");
$total += &runTest ("29", "Ensure that time for link is NOT used when -L");
$total += &runTest ("30", "Multiple -newer's");
$total += &runTest ("31", "Ensure that time for file is evaluated before DFS");

&header ("-print");
$total += &runTest ("32", "Multiple -print's");

&header ("-a / -o");
$total += &runTest ("33", "First argument (error)");
$total += &runTest ("34", "Last argument (error)");
$total += &runTest ("35", "Adjacent arguments (error)");
$total += &runTest ("36", "Options are only operands preceding -a/-o (no error)");

&header ("Detect loops");
$total += &runTest ("37", "Long loop");
$total += &runTest ("38", "A, A/B directories; A/B/S -> A; find -L A/B");
$total += &runTest ("39", "False loops");
$total += &runTest ("40", "One-hop loop");

&header ("Errors");
$total += &runTest ("41", "-P/-L invalid if do not precede files and actions");
$total += &runTest ("42", "Invalid maxdepth: Not nonnegative");
$total += &runTest ("43", "Invalid maxdepth: Not integer (+2)");
$total += &runTest ("44", "Missing ; or command following -exec");
$total += &runTest ("45", "Missing argument following -newer/-name/-maxdepth");
$total += &runTest ("46", "Nonexistent/unreadable file: Argument to -newer");
$total += &runTest ("47", "opendir() fails (directory is not go+x)");

&header ("Torture");
$total += &runTest ("48", "No files (default .) or no arguments");
$total += &runTest ("49", "Multiple options or no actions (default is -print)");
$total += &runTest ("50", "Actions in one -o clause inhibit -print in others");
$total += &runTest ("51", "Unusual names");
$total += &runTest ("52", "-print/-exec -depth/-maxdepth (one error)");
$total += &runTest ("53", "-newer/-name -depth -maxdepth (two errors)");
$total += &runTest ("54", "Trailing /'s");
$total += &runTest ("55", "Multiple trailing /'s");

&header ("Free all storage except on immediate exit");
$total += &runTest ("56", "Test #06: -exec with one {}");
$total += &runTest ("57", "Test #08: Local file newer or older");
$total += &runTest ("58", "Test #11: Two-hop loop");
$total += &runTest ("59", "Test #15: No action specified ");
$total += &runTest ("60", "Test #07: Regular expression matches");

# # &header ('Deductions for Violating Specification (0 => no violation)');

printf ("\n%3d points Total for $NAME\n", $total);

# &header ("Non-credit Tests");
# $total += &runTest ('61', 'Time for file is evaluated before DFS');

&sectionHeader ("Possible Deductions (assessed later as appropriate)");
&possibleDeduction ( -5, "Does not make");
&possibleDeduction ( -1, "Makefile missing");
&possibleDeduction ( -1, "Makefile incorrect");
&possibleDeduction ( -1, "Log file incorrectly named");
&possibleDeduction ( -1, "Log file lacks estimated time");
&possibleDeduction ( -1, "Log file lacks total time");
&possibleDeduction ( -1, "Log file lacks statement of major difficulties");
&possibleDeduction ( -1, "Compilation errors using -Wall -std=c99 -pedantic");

if ($TYPE eq "Final") {
   print "\n";
   system ("rm -f $PROGRAM *.o")                # Cleanup if final script
      unless ($LANG eq "Perl");
}

if ($TYPE eq "Public") {                        # Reminder to students
   system ("/c/cs$CLASS/bin/checklog -noprint");
   system ("/c/cs$CLASS/bin/checkmake -noprint")
      unless ($LANG eq "Perl");
}

exit $total;


##########
# Print section header for tests
sub sectionHeader {
   printf ("\n%11s%s\n", "", @_);
}


##########
# Print header for tests
sub header {
   printf ("\n%15s%s\n", "", @_);
}


##########
# Print addition
sub addition {
   my ($points, $title) = @_;
   printf ("%3d point       $title\n", $points);
   return $points;
}


##########
# Print deduction
sub deduction {
   my ($points, $title) = @_;
   printf ("%3d point       $title\n", $points);
   return $points;
}


##########
# Print possible deduction
sub possibleDeduction {
   printf ("%18d %s\n", @_);
}


##########
# Run a test
sub runTest {
   my ($test, $title, $conds) = @_;
   my $where   = "/c/cs$CLASS/Hwk$HWK/Tests";   # Where to find test files
   my $run     = "/c/cs$CLASS/bin/run";         # Utility to run program
   my $diff    = "/usr/bin/diff";               # Utility to compare files
   my $head    = "/c/cs$CLASS/bin/Head";        # Utility to output files
   my $results = "/tmp/$NAME.$$";               # Results of test
   my $diffs   = "/tmp/diff.$$";                # Expected results vs. results
   my $errors  = "/tmp/errs.$$";                # Error messages generated
   my ($status, @conds, $points);

   my $testFile = "$where/$TEST";               # Name of test file
   $testFile    =~ s{IJ}{$test};
   my $answers  = "$where/$ANSWER";             # Name of answer file
   $answers     =~ s{IJ}{$test};

   return 0                                     # Either execute all tests or
      unless (keys %WHICH == 0                  #   only those on command line
	      || exists $WHICH{$test});

   (-r $testFile)
      || die ("$0: missing test file $testFile\n");

   push @UNLINK, $results, $errors, $diffs;     # Files to delete on signal

   if (-x $testFile) {
      $status = execute ($testFile, undef, $results, $errors);
   } elsif ($LANG eq "Perl") {
      $status = execute ($PROGRAM, $testFile, $results, $errors);
   } else {
      $status = execute ("$run $PROGRAM", $testFile, $results, $errors);
   }

   if (defined $conds && $conds eq "Graceful") {
      @conds = ('NORMAL', $status);
   } else {
      (-r $answers)
	 || die ("$0: missing answer file $answers\n");
      system ("$diff $answers $results  > $diffs  2>> $errors");
      system ("$head $errors $diffs");
      @conds = ('NULL', $errors,  'NULL', $diffs);
   }

   if (defined $conds && $conds eq "Error message") {
      @conds = ('NONNULL', $errors,  'NULL', $diffs);
   }

   if (defined $conds && $conds =~ m{^Deduct=(\d+)$}) {
      @conds = ('DEDUCT', $1, @conds);
   }

   $points = &correct (@conds);
   printf ("%3d point  %3s. %s\n", $points, $test, $title);
   system ("rm -f $results $errors $diffs");
   pop @UNLINK;  pop @UNLINK;  pop @UNLINK;     # Remove added files

   return $points;
}


##########
#  correct ({[UNOP FILE] | ['NORMAL' STATUS]}*)
#
#  Return 1 if the conjunction of the specified tests is true, else 0, where:
#
#    UNOP FILE (where UNOP is either 'NULL' or 'NONNULL'):
#      Is the size of the file FILE as specified?
#
#    'NORMAL' STATUS:
#      Did the process terminate normally?
#
#    'DEDUCT' POINTS:
#      Change the point values to 0 for success, -POINTS for failure
#
sub correct {
   my $op;
   my ($success, $failure) = (1, 0);

   while ($op = shift) {
      if ($op eq 'NULL') {
	 my $arg = shift;
	 print STDERR "$op $arg\n" if $debug;
	 if (-s $arg) {
	    if ($arg =~ m{/diff\.}) {
	       print "Error: STDOUT differs from expected\n";
	    } elsif ($arg =~ m{/errs\.}) {
	       print "Error: STDERR should be empty\n";
	    } else {
	       print "Error: File $arg is nonempty\n";
	    }
	    return $failure;
	 }

      } elsif ($op eq 'NONNULL') {
	 my $arg = shift;
	 print STDERR "$op $arg\n" if $debug;
	 if (!-s $arg) {
	    if ($arg =~ m{/errs\.}) {
	       print "Error: STDERR should be nonempty\n";
	    } else {
	       print "Error: File $arg is empty\n";
	    }
	    return $failure;
	 }

      } elsif ($op eq 'NORMAL') {
	 my $arg = 127 & shift;
	 print STDERR "$op $arg\n" if $debug;
	 if ($arg != 0) {
	    print "Error: Status = $arg is nonzero\n";
	    return $failure;
	 }

      } elsif ($op eq 'DEDUCT') {
	 my $arg = shift;
	 ($success, $failure) = (0, -$arg);
      }
   }
   return $success;
}


##########
# Create program to test
sub makeProgram {
#  system ("rm -f $PROGRAM");                   # Delete program & object files
#  system ("rm -f *.o")
#     if ($TYPE eq "Final");

   (-f "Makefile" || -f "makefile")             # Give warning if no Makefile
      || warn ("$0: no makefile found\n");

   system ("/c/cs323/bin/makewarn $PROGRAM");
   ($? == 0)
      || die ("$0: cannot compile $PROGRAM\n");

   for my $p (split (" ", $PROGRAM)) {
      -e $p
	 || die ("$0: could not make $p\n");
   }
}


##########
# Limit CPU-time, wall-clock-time, file-size, and/or heap-size
use BSD::Resource;

sub limitCpuTime { # (time in seconds)
   my ($soft, $hard) = @_;
   $hard = $soft
      if (! defined($hard));
   setrlimit (RLIMIT_CPU, $soft, $hard);
}

sub limitWallClock { # (time in seconds)
   my ($wall) = @_;
   $SIG{ALRM} = 'IGNORE';                       # Parent ignores alarms
   $WallClock = $wall;
}

sub limitFileSize { # (size in kilobytes)
   my ($size) = @_;
   $size *= 1024;
   setrlimit (RLIMIT_FSIZE, $size, $size);
}

sub limitHeapSize { # (size in kilobytes        # Bug: Has no effect
   my ($size) = @_;
   $size *= 1024;
   setrlimit (RLIMIT_VMEM, $size, $size);
}

sub limitProcesses { # (#processes)             # Bug: Has no effect
   my ($nproc) = @_;
   setrlimit (RLIMIT_NPROC, $nproc, $nproc);
}


##########
# Execute program after redirecting stdin, stdout, & stderr and return status
sub execute {
   my ($program, $stdin, $stdout, $stderr) = @_;
   my ($pid, $status);

   (defined ($pid = fork))                      # Create child process
      || die ("$0: fork failed\n");

   if ($pid == 0) {                             # Child process
      open (STDIN, "<$stdin")                   #  Redirect stdin
	 if (defined $stdin);
      open (STDOUT, ">$stdout")                 #  Redirect stdout
	 if (defined $stdout);
      open (STDERR, ">$stderr")                 #  Redirect stderr
	 if (defined $stderr);
      mkdir ($TMPDIR)                           #  Create a temporary directory
	 || die ("$0: mkdir $TMPDIR failed\n");
      system ("/bin/cp $PROGRAM $TMPDIR");      #    With a copy of the program
      chdir ("$TMPDIR")                         #    And cd there
	 || die ("$0: chdir $TMPDIR failed\n");
      (exec $program)                           #  Execute command
	 ||  die ("$0: exec failed\n");
   }

   alarm ($WallClock);                          # Set an alarm to interrupt in
   $SIG{ALRM} =                                 # ... $WallClock seconds
      sub {kill "TERM", $pid;
	   if (defined $stderr) {
	      open (ERROR, ">>$stderr")
		 || die ("$0: open (>>$stderr) failed\n");
	      print ERROR  "Time limit exceeded\n";
	      close (ERROR);
	   } else {
	      print STDERR "Time limit exceeded\n";
	   }
      };
   waitpid ($pid, 0);                           # Wait for child to die,
   alarm (0);                                   # ... cancel alarm,
   $status = $?;

   system ("/bin/chmod -R +w $TMPDIR");         # Delete temporary directory
   system ("/bin/rm -rf $TMPDIR");
   (! -e $TMPDIR)
      || die ("$0: cannot delete $TMPDIR\n");

   return $status;                              # ... and return exit status
}


##########
# $FILES is a blank-separated list of filenames which may include wildcards.
# If any of these files exist in the current working directory, print their
# names and return -1; else return 0.
sub localCopies { # ($files)
   my ($files) = @_;
   open (LS, "ls -l $files 2>/dev/null |")
       || die ("$0: cannot ls -l $files\n");
   my @ls = <LS>;
   close (LS);
   print @ls;
   return (@ls > 0) ? -2 : 0;
}
